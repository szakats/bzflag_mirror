# ------------------------------------------------------------------------
#  BZWGen 0.1.2 grammar file
#  build.set -- standard buildings
# ------------------------------------------------------------------------

build_preprocess
  -> 0.4 : (face(h) > 60) partitionh(face(h)/2 - 2.5) [ build_preprocess ] partitionh(5.0) build_preprocess
  -> 0.4 : (face(v) > 60) partitionv(face(v)/2 - 2.5) [ build_preprocess ] partitionv(5.0) build_preprocess
  -> spawn(build_run)
;

build_run
  -> 0.2 : sky_run
  -> 0.2 : assign(TEXSNAP = 4) assign(TEXTILE = 0.5) assign(BUILD_MAT = $MATWALL)  build_1
  -> 0.6 : assign(TEXSNAP = 4) assign(TEXTILE = 0.5) assign(BUILD_MAT = $MATWALL2) build_2
;

build_2 
  -> 0.3 : mat_glass extrudet(4.0) [ build_entry * build_entry * ] build_cont
  -> 0.3 : mat_glass extrudet(4.0) [ * build_entry * build_entry ] build_cont
  -> 0.2 : mat_wall2 extrudet(3.7) [ * build_entry * build_entry ] extrude(0) expand(1.0) build_cont
  -> 0.1 : mat_wall2 extrudet(random(12.0,40.0,4.0)) [ * build_insetwall * build_insetwall ] roof_run
  -> 0.1 : mat_wall2 extrudet(random(12.0,40.0,4.0)) [ build_insetwall * build_insetwall ] roof_run
;

build_1
  -> 0.3 : mat_wall extrudet(4.0) [ build_entry * build_entry * ] build_cont
  -> 0.3 : mat_wall extrudet(4.0) [ * build_entry * build_entry ] build_cont
  -> 0.2 : mat_wall extrudet(random(12.0,40.0,4.0)) [ * build_insetwall * build_insetwall ] build_roof 
  -> 0.2 : mat_wall extrudet(random(12.0,40.0,4.0)) [ build_insetwall * build_insetwall * ] build_roof 
;

build_entry -> partitionh(face(h)/2 - 2.0) [ common_texture ] partitionh(4.0) [ build_entrydoor ]  texture();

build_entrydoor 
  -> ($BUILD_MAT = $MATWALL2) mat_mesh extrude(-2.0) mat_doorw texturefull()
  -> ($BUILD_MAT = $MATWALL)  mat_roof extrude(-2.0) mat_doorr texturefull()
  -> mat_roof extrude(-2.0) 
; 

build_cont
  -> 0.5 : common_ledge extrude(3.7) [ @build_detail ] build_cont
  -> 0.2 : common_ledge extrude(3.7) [ @build_insetwall ] build_cont
  -> 0.1 : (face(h) > 100) common_ledge build_parth build_cont
  -> 0.1 : (face(v) > 100) common_ledge build_partv build_cont
  -> 0.3 : build_roof
;

build_roof 
  -> 0.5 : (face(s) < 20000 & face(z) < 20) mat_mesh extrude(0) expand(0.3) mat_rooft extrude(5) expand(-5) mat_roof
  -> roof_run
;


build_parth
  -> partitionh(face(h)/3)    material($BUILD_MAT) extrudet(3.7)
  -> partitionhi(2*face(h)/3) material($BUILD_MAT) extrudet(3.7)
;

build_partv
  -> partitionv(face(v)/3)    material($BUILD_MAT) extrudet(3.7)
  -> partitionvi(2*face(v)/3) material($BUILD_MAT) extrudet(3.7)
;

build_insetwall -> subdivideh(3) [ build_detail build_inset build_detail ] ;

build_inset 
  -> 0.8 : material($BUILD_MAT) extrudet(-3.0) texture() mat_glass
  -> 0.2 : material($BUILD_MAT) extrudet(-3.0) texture()
;

build_detail 
  -> ($BUILD_MAT = $MATWALL2 & $DETAIL > 2) mat_wall2 textureclear() subdivideh(face(h)/4) [ @build_window ]
  -> texture()
;

build_window -> extrude(0) expand(-0.4) extrude(-0.2);

  
# ------------------------------------------------------------------------
#  end of file
# ------------------------------------------------------------------------